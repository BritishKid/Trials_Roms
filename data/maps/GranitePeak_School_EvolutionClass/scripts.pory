mapscripts GranitePeak_School_EvolutionClass_MapScripts {
     MAP_SCRIPT_ON_TRANSITION {
            setflag(FLAG_HIDE_EVOLUTION_ROOM)
        if (var(VAR_EVOLUTION_ROOM_STUDENTS) == 4) {
            clearflag(FLAG_HIDE_EVOLUTION_ROOM)
        }
    }
}

script GranitePeak_EvolutionClass_Teacher {
    lockall
    faceplayer
    //to do turn this into a stone shop
    if (!flag(FLAG_GET_SCHOOL_EEVEE) && flag(FLAG_EVOLUTION_PATCH_EARNT)) {
        call(GranitePeak_GivePlayer_Eevee)
    }
    if(var(VAR_EVOLUTION_ROOM_STUDENTS) == 5) {
        msgbox(format("Remember you need all patches to take part in the tournament."))
    }
    if(var(VAR_EVOLUTION_ROOM_STUDENTS) == 4) {
        call(EvolutionTest)
    }
    if(var(VAR_EVOLUTION_ROOM_STUDENTS) == 3) {
        msgbox(format("Oh it looks like you've learnt lots, are you ready for the test?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES) {
                setvar(VAR_EVOLUTION_ROOM_STUDENTS, 4)
                msgbox(format("HANG ON!"))
                applymovement(1, Movement_TeacherSurprised) //they move into wall
                waitmovement(1)
                addobject(5)
                setobjectxy(5,5,10)
                applymovement(5, Movement_MoveUpFive)
                waitmovement(5)
                addobject(6)
                setobjectxy(6,6,10)
                applymovement(6, Movement_MoveUpFive)
                waitmovement(6)
                applymovement(5, Movement_Bounce)
                msgbox(format("We want to take part to!"))
                applymovement(6, Movement_Bounce)
                msgbox(format("Yeah! I want to win the tournament so I need these patches."))
                msgbox(format("Okay very well, let's test all three of you then"))
                call(EvolutionTest)
            }
            if(var(VAR_RESULT) == NO) {
                msgbox(format("Okay dear, come back when you're ready."))
            }
    }
    if (var(VAR_EVOLUTION_ROOM_STUDENTS) < 2) {
        msgbox(format("Pokémon can evolve in many different ways. Some use stones, some evolve after battle experience and others evolve from their surrounding area."))
        msgbox(format("You've still got some studying to do before I can give you your patch."))
    } 
    releaseall
}

script EvolutionTest {
    setvar(VAR_PREVENT_LOOPS_SCHOOL, 0)
    msgbox(format("Question 1. Eevee is the only Pokémon with multiple evolutions?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO && var(VAR_PREVENT_LOOPS_SCHOOL) == 0) {
        applymovement(5, Movement_Bounce)
        waitmovement(5)
        msgbox(format("What about Wurmple!"))
        applymovement(6, Movement_Bounce)
        waitmovement(6)
        msgbox(format("And Snorunt"))
        msgbox(format("That's correct many Pokémon other than Eevee can evolve in different ways."))   
        msgbox(format("Question 2. Are there any Pokémon that can evolve twice at once?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES && var(VAR_PREVENT_LOOPS_SCHOOL) == 0) {
            applymovement(5, Movement_Bounce)
            waitmovement(5)
            msgbox(format("Nincada!"))
            msgbox(format("That's correct, Nincada evolves into both Shedinja and Ninjask"))
            msgbox(format("You're doing great, okay final question"))
            msgbox(format("Do some Pokémon only evolve through trade?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES && var(VAR_PREVENT_LOOPS_SCHOOL) == 0) {
                msgbox(format("I'm sorry that's wrong. Keep reading up on evolution and you'll get there"))
                call(FailedEvolutionTest)
            }
            if(var(VAR_RESULT) == NO && var(VAR_PREVENT_LOOPS_SCHOOL) == 0) {
                msgbox(format("Correct with the discovery of a Link Stone you no longer have to trade certain Pokémon."))
                call(CompletedEvolutionTest)
            }
        }
        if(var(VAR_RESULT) == NO && var(VAR_PREVENT_LOOPS_SCHOOL) == 0)  {
            applymovement(6, Movement_Bounce)
            waitmovement(6)
            msgbox(format("That's stupid how can a Pokémon evolve into two Pokémon?"))
            msgbox(format("I'm sorry that's incorrect. Keep learning then come back and try again."))
            call(FailedEvolutionTest)
        }
    }
    if(var(VAR_RESULT) == YES && var(VAR_PREVENT_LOOPS_SCHOOL) == 0) {
        applymovement(5, Movement_Bounce)
        waitmovement(5)
        msgbox(format("What about Wurmple!"))
        msgbox(format("Correct, Eevee is just one of many"))
        msgbox(format("Maybe you should study some more and try again."))
        call(FailedEvolutionTest)
    }
}

script CompletedEvolutionTest {
    setvar(VAR_PREVENT_LOOPS_SCHOOL, 1)
    msgbox(format("Congratulations to you all you've passed. Here take these."))
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("Recieved Evolution Patch. This shows you've passed the class."))
    msgbox(format("Remember you need all patches to take part in the tournament."))
    setvar(VAR_EVOLUTION_ROOM_STUDENTS, 5)
    setflag(FLAG_EVOLUTION_PATCH_EARNT)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_POKEMON_LEAGUE, SUB_QUEST_SCHOOL_EVOLUTION_PATCH)
    applymovement(5, Movement_Bounce)
    waitmovement(5)
    msgbox(format("Bet you're happy I was here, I carried you through that."))
    applymovement(5, Movement_Leaves_Room)
    waitmovement(5)
    removeobject(5)
    applymovement(6, Movement_Bounce)
    waitmovement(6)
    applymovement(6, Movement_Leaves_Room)
    waitmovement(6)
    removeobject(6)
    call(GranitePeak_GivePlayer_Eevee)
    releaseall
}

script FailedEvolutionTest {
    msgbox(format("There are lots of resources you can use to study with."))
    setvar(VAR_PREVENT_LOOPS_SCHOOL, 1)
    releaseall
}

script GranitePeak_EvolutionClass_Boy1 {
    trainerbattle_single(TRAINER_EVOLUTIONBOY1, EvolutionBoy1_Intro, EvolutionBoy1_Outro, EvolutionStudent_Beaten)
    msgbox(PostBattle_EvolutionBoy1, MSGBOX_AUTOCLOSE)
    end
}

script GranitePeak_EvolutionClass_Boy2 {
    trainerbattle_single(TRAINER_EVOLUTIONBOY2, EvolutionBoy2_Intro, EvolutionBoy2_Outro, EvolutionStudent_Beaten)
    msgbox(PostBattle_EvolutionBoy2, MSGBOX_AUTOCLOSE)
    end
}

script GranitePeak_EvolutionClass_Girl {
    trainerbattle_single(TRAINER_EVOLUTIONGIRL, EvolutionGirl_Intro, EvolutionGirl_Outro, EvolutionStudent_Beaten)
    msgbox(PostBattle_EvolutionGirl, MSGBOX_AUTOCLOSE)
    end
}

script GranitePeak_GivePlayer_Eevee {
    showmonpic(SPECIES_EEVEE, 10, 3)
    givemon(SPECIES_EEVEE, 5)
    playfanfare(MUS_OBTAIN_ITEM)
    if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
            msgbox("Recieved Eevee")
            call(NicknamePartyMon)
        }
        elif (var(VAR_RESULT) == MON_GIVEN_TO_PC) {
            msgbox("Recieved Eevee")
            call(NicknameBoxMon)
        }
        else {
            hidemonpic
            msgbox(format("You don't have enough space. Come back and you get it soon."))
    }
    setflag(FLAG_GET_SCHOOL_EEVEE)
}

script EvolutionStudent_Beaten {
    if (var(VAR_EVOLUTION_ROOM_STUDENTS) == 2) {
        setvar(VAR_EVOLUTION_ROOM_STUDENTS, 3)
    }
    if (var(VAR_EVOLUTION_ROOM_STUDENTS) == 1) {
        setvar(VAR_EVOLUTION_ROOM_STUDENTS, 2)
    }
    if (var(VAR_EVOLUTION_ROOM_STUDENTS) == 0) {
        setvar(VAR_EVOLUTION_ROOM_STUDENTS, 1)
    }
    call(Increase_Trainers_Beaten)
}

//movement scripts

movement Movement_TeacherSurprised {
    emote_exclamation_mark    
}

movement Movement_MoveUpFive {
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
}

movement Movement_Bounce {
    jump_in_place_up
}

movement  Movement_Leaves_Room {
    walk_fast_down
    walk_fast_down
    walk_fast_down
    walk_fast_down
    walk_fast_down
}

//text scripts
text EvolutionBoy1_Intro {
    format("Did you know Pokémon can learn move faster by not being evolved.")
}

text EvolutionBoy1_Outro {
    format("It can help make a Pokémon stronger.")
}

text PostBattle_EvolutionBoy1 {
    format("Some evolution lines can only learn moves before they evolve.")
}

text EvolutionBoy2_Intro {
    format("You can use some items to evolve Pokémon, but did you know some items can be used to stop evolution or make non evolved Pokémon stronger.")
}

text EvolutionBoy2_Outro {
    format("Wow, I thought they would be unstoppable.")
}

text PostBattle_EvolutionBoy2 {
    format("Eviolite is an incredible item that improves defence on Pokémon that can evolve.")
}

text EvolutionGirl_Intro {
    format("Some Pokémon can evolve in many different ways.")
}

text EvolutionGirl_Outro {
    format("Losing doesn't help them evolve though.")
}

text PostBattle_EvolutionGirl {
    format("A Pokémon can complete change depending on how it evolves.")
}
