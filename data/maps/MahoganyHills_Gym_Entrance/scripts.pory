mapscripts MahoganyHills_Gym_Entrance_MapScripts {
}

script EventScript_MahoganyHills_MeetRob {
    lockall
    msgbox(format("So it's you!"))
    removeobject(1)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_MahoganyHills_Robert_ExclamationMark)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    addobject(1)
    setobjectxy(1,16,20)
    applymovement(1, Movement_MahoganyHills_Robert_Approaches)
    waitmovement(1)
    msgbox(format("I've been hearing word of a new training beating everyone."))
    clearflag(FLAG_HIDE_FIRST_ENCOUNTER_ROB)
    if(var(VAR_TRAINERS_BEATEN_CHECK) >= 10){ //Beat atleast 10 trainers to trigger meeting Robert
        msgbox(format("It must be you, everyones been saying about how you destroy them in battle. \p Well how about it, care to show me how strong you are?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES) {
                trainerbattle_single(TRAINER_FIRST_ENCOUNTER_ROBERT, MahoganyHills_Robert_Intro, MahoganyHills_Robert_Outro, EventScript_MahoganyHills_RobertBeaten)
            }
            else{
                msgbox(format("Oh that's a shame. I'll see you another time then."))
                call(EventScript_MahoganyHills_RobertMoves)
            }
    } else {
        msgbox(format("But no it's not you, you're not the one they are talking about. \p If you find out who's been winning against all the trainers around here please come find me so I can see how strong they are."))
        call(EventScript_MahoganyHills_RobertMoves)
    }
    releaseall
}

script EventScript_MahoganyHills_RobertBeaten{ 
    msgbox(format("Wow you're incredible, here take this!"))
    giveitem(ITEM_ABILITY_PATCH, 1)
    msgbox(format("You can use it to bring out the Hidden Ability for your Pokémon.\p It can make your Pokémon much stronger!"))
    if(var(VAR_ROBERT_STATE) == 0)  {
        msgbox(format("Anyway I'll catch you later!"))
        call(EventScript_MahoganyHills_RobertLeaves)
    }
    setvar(VAR_ROBERT_STATE, 2)
    setflag(FLAG_HIDE_FIRST_ENCOUNTER_ROB)
    releaseall
}

script EventScript_MahoganyHills_RobertMoves {
    applymovement(1, Movement_MahoganyHills_Robert_Moves)
    waitmovement(1)
    setobjectxy(1, 18, 16)
    setvar(VAR_ROBERT_STATE, 1)
}

script EventScript_MahoganyHills_RobertLeaves {
    applymovement(1, Movement_MahoganyHills_Robert_Leaves)
    waitmovement(1)
}

script EventScript_MahoganyHills_Robert_FindLater {
    if(var(VAR_ROBERT_STATE)==1) {
        msgbox(format("Oh it's you.\n Hello"))
        if(var(VAR_TRAINERS_BEATEN_CHECK) >= 10){ //Beat atleast 10 trainers to trigger meeting Robert
            msgbox(format("I've heard you've been winning alot of battles.\p But do you think you're good enough to beat me?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES) {
                    trainerbattle_single(TRAINER_FIRST_ENCOUNTER_ROBERT, MahoganyHills_Robert_Intro, MahoganyHills_Robert_Outro, EventScript_MahoganyHills_RobertBeaten)
            }
        } else {
            msgbox(format("I'm still trying to find that strong trainer"))
        }
    } 
    if(var(VAR_ROBERT_STATE) == 2) {
        msgbox(format("I'll train some more and beat you by the Coral gym."))
    }
    releaseall
}

script Gym1_Sign {
    lockall
    msgbox(format("Mahogany Gym: Can you handle the sharp talons in the wind"))
    releaseall
}


//movement scripts
movement Movement_MahoganyHills_Robert_ExclamationMark {
    emote_exclamation_mark
}

movement Movement_MahoganyHills_Robert_Approaches {
    walk_up
    walk_up
    walk_up
    walk_up
}

movement Movement_MahoganyHills_Robert_Leaves {
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
}

movement Movement_MahoganyHills_Robert_Moves {
    walk_right
    walk_right
    face_down
}

//text
text MahoganyHills_Robert_Intro {
    format("Prepare yourself, I won't hold back")
}

text MahoganyHills_Robert_Outro {
    format("Just incredible!")
}